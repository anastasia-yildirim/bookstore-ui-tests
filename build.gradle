plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
    id "io.freefair.lombok" version "8.10.2"
}

repositories {
    mavenCentral()
}

def restAssuredVersion = "5.5.0",
    allureVersion = "2.29.0",
        aspectjWeaverVersion = "1.9.22",
        selenideVersion = "7.5.1",
        junitVersion = "5.11.3",
        slf4jVersion = "2.0.16",
        ownerVersion = "1.0.12",
        assertjVersion = "3.26.3"


allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        aspectjVersion.set(aspectjWeaverVersion)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "com.codeborne:selenide:$selenideVersion",
            "org.aspectj:aspectjweaver:$aspectjWeaverVersion",
            "org.assertj:assertj-core:$assertjVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
    )
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}